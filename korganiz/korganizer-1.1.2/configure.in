# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.

AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(korganizer, 1.1.2) dnl searches for some needed programs

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS

dnl Checks if we want to use insure++ to debug
KDE_CHECK_INSURE

# the following is to allow programs, that are known to 
# have problems when compiled with -O2
if test -n "$CXXFLAGS"; then
  safe_IFS=$IFS
  IFS=" "
  NOOPT_CXXFLAGS=""
  for i in $CXXFLAGS; do
    if test "$i" = "-O2"; then
      i=""
    fi
    NOOPT_CXXFLAGS="$NOOPT_CXXFLAGS $i"
  done
  IFS=$safe_IFS
fi
AC_SUBST(NOOPT_CXXFLAGS)
AM_DISABLE_LIBRARIES
KDE_PROG_LIBTOOL

AC_PROG_YACC

KDE_FIND_PATH(perl, PERL, [], 
	[
	  AC_MSG_WARN([Couldn't find perl 5 or later. ical2vcal will no work.])		],
	[-e 'require 5.000;']
)
	
AC_SUBST(PERL)

dnl WITH_NLS is for the po files
AM_KDE_WITH_NLS
AC_PATH_KDE

dnl Checks for header files.
AC_LANG_CPLUSPLUS
# check for the version of the standard template library for this c++ compiler.
KDE_CHECK_STL
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/select.h \
	sys/ioctl.h sys/stropts.h malloc.h sys/sockio.h \
	sys/m_wait.h sys/param.h libgen.h)
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
AC_LANG_C
AC_C_LONG_DOUBLE
AC_TYPE_GETGROUPS

dnl Checks for library functions.
AC_CHECK_FUNCS(socket strdup vsnprintf basename)
AC_CHECK_SETENV
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_USLEEP

dnl output files
topdir=`pwd`
AC_SUBST(topdir)

dnl Perform program name transformation
AC_ARG_PROGRAM

dnl     Replace line:
dnl	src/libkab/Makefile \
dnl 	To re-enable support of addressbook library.

AC_OUTPUT(Makefile \
        src/Makefile \
        src/versit/Makefile \
	src/pixmaps/Makefile \
	src/holidays/Makefile \
        src/doc/Makefile \
        src/doc/en/Makefile \
	src/sounds/Makefile \
        po/Makefile \
	src/ical2vcal)
